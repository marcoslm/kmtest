[ClassId("57858BD8-4A90-461A-94F2-6D6D5F855A36")]
[InterfaceId("629884A1-C2C5-4A32-AA4C-3733648E913D")]
[COMCreatable(False)]
Private Class BaseControlWindowlessNoFocus

    Implements BaseControlRectDockable Via BaseControlRectDockable

    [Description("")]
    [Serialize(True, "Visible")]
        Private VisibleINIT As Boolean = True
        
    [Serialize(True, "DragIcon")]
    [CustomDesigner("designer_PictureBytes")]
    [Description("")]
        Private ReadOnly DragIconINIT() As Byte
    [Serialize(True, "DragMode")]
        Private DragModeINIT As VBRUN.DragModeConstants
    
    Sub New(ByVal ControlType As ControlTypeConstants)
        Set Me.BaseControlRectDockable = New BaseControlRectDockable(ControlType)
    End Sub

    Friend Function CreateRootWindowElement(ByVal _ControlContext As stdole.IUnknown, _
                                                ByRef InitData As WindowCreationData) As WindowElement
        With CType(Of WinNativeFormsBASE.BaseControlInfo)(Me.InternalBaseControlInfoPtrX)
            .WindowlessVisible = Me.VisibleINIT
            .DragMode = Me.DragModeINIT
        End With
        InitData.BaseControlInfoPtr = InternalBaseControlBeforeCreateRootWindow(InitData)
        Set Me.ControlContext = CType(Of WindowsControlContext)(_ControlContext) ' FIXME passed as IUnknown to prevent COM exposure of type
        Set Me.RootWindowElementBase = CType(Of WindowElement)(Me.ControlContext.CreateWindowElement(InitData))
        CommonLoadPictureInit(CType(Of WinNativeFormsBASE.BaseControlInfo)(Me.InternalBaseControlInfoPtrX).DragIcon, Me.DragIconINIT)
        Return Me.RootWindowElementBase
    End Function
            
    Public Sub Drag([TypeHint(DragConstants)] Optional ByVal Action As Variant)
        CommonDrag(Action)
    End Sub

    Public Sub Refresh()
        WindowlessRefresh()
    End Sub
    
    Public Sub ZOrder([TypeHint(ZOrderConstants)] Optional ByVal Position As Variant)
        CommonZOrderWindowless(Position, Me.ControlContext)
    End Sub
    
    [Serialize(False)]
    Property Get Visible() As Boolean
        Return CType(Of WinNativeFormsBASE.BaseControlInfo)(Me.InternalBaseControlInfoPtrX).WindowlessVisible
    End Property
        
    [Serialize(False)]
    Property Let Visible(ByVal Value As Boolean)
        CType(Of WinNativeFormsBASE.BaseControlInfo)(Me.InternalBaseControlInfoPtrX).WindowlessVisible = Value
        WindowlessRefresh()
    End Property
    
    [Serialize(False)]
    Public Property Get DragMode() As DragModeConstants
        Return CType(Of WinNativeFormsBASE.BaseControlInfo)(Me.InternalBaseControlInfoPtrX).DragMode
    End Property
    
    [Serialize(False)]
    Public Property Let DragMode(ByVal Value As DragModeConstants)
        CType(Of WinNativeFormsBASE.BaseControlInfo)(Me.InternalBaseControlInfoPtrX).DragMode = Value
    End Property
    
    [Serialize(False)]
    Public Property Get DragIcon() As StdPicture
        Return CType(Of BaseControlInfo)(Me.InternalBaseControlInfoPtrX).DragIcon
    End Property

    [Serialize(False)]
    Public Property Set DragIcon(Value As StdPicture)
        Set CType(Of BaseControlInfo)(Me.InternalBaseControlInfoPtrX).DragIcon = Value
    End Property

    [Serialize(False)]
    Public Property Let DragIcon(Value As StdPicture)
        Set CType(Of BaseControlInfo)(Me.InternalBaseControlInfoPtrX).DragIcon = Value
    End Property
End Class